#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Boss testing"]
#Text["feat. dangerous ocean fairy"]

#include "script/main/script/lib/DarkShotconstants.dnh"
#include"script/default_system/Default_Effect.txt"

let objBoss;
@Initialize
{
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	TWork;
	Animation_Azure(objBoss);
	BossAura_Azure(objBoss);
	TEnd;

	DeleteShotAll(TYPE_ALL, TYPE_ITEM);
}

@MainLoop
{
	yield;
}

@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(1500);
	}
	case(EV_REQUEST_TIMER)
	{
		SetScriptResult(60);
	}
}

task TWork
{
	ObjMove_SetPosition(objBoss,192,120);
	wait(120);
	Obj_SetValue(objBoss,"Anim",1);
	wait(120);
	Obj_SetValue(objBoss,"Anim",0);
	yield;
	Obj_SetValue(objBoss,"Anim",2);
	wait(120);
	Obj_SetValue(objBoss,"Anim",3);
	wait(120);
	Obj_SetValue(objBoss,"Anim",0);
}

task Animation_Azure(obj){
	let blink = false;
	BlinkTimer;
	ObjSprite2D_SetDestRect(obj,-32,-48,32,48);
	ObjPrim_SetTexture(obj,GetCurrentScriptDirectory() ~ "./system/azuresprite.png");
	while(!Obj_IsDeleted(obj)){
		while(Obj_GetValueD(obj,"Anim",0) == 0 && !Obj_IsDeleted(obj)){
			if(blink){
				blink = false;
				ObjSprite2D_SetSourceRect(obj,256,0,320,96);
				wait(6);
				ObjSprite2D_SetSourceRect(obj,0,0,64,96);
				wait(6);
			}
			else{
				ObjSprite2D_SetSourceRect(obj,0,0,64,96);
				wait(12);
			}
			ascent(i in 1..4){
				if(Obj_GetValueD(obj,"Anim",0) != 0 || Obj_IsDeleted(obj)){break;}
				ObjSprite2D_SetSourceRect(obj,i*64,0,(i+1)*64,96);
				wait(12);
			}
		}
		if(Obj_GetValueD(obj,"Anim",0) == 1){
			ObjRender_SetScaleX(obj,-1);
			Obj_SetValue(obj,"Anim",2);
		}
		if(Obj_GetValueD(obj,"Anim",0) == 2 && !Obj_IsDeleted(obj)){
			ascent(i in 0..4){
				ObjSprite2D_SetSourceRect(obj,i*64,96,(i+1)*64,192);
				wait(6);
			}
			while(Obj_GetValueD(obj,"Anim",0) == 2 && !Obj_IsDeleted(obj)){
				if(blink){
					blink = false;
					ObjSprite2D_SetSourceRect(obj,256,96,320,192);
					wait(6);
					ObjSprite2D_SetSourceRect(obj,192,96,256,192);
				}
				yield;
			}
			descent(i in 0..4){
				ObjSprite2D_SetSourceRect(obj,i*64,96,(i+1)*64,192);
				wait(6);
			}
			ObjRender_SetScaleX(obj,1);
		}
		if(Obj_GetValueD(obj,"Anim",0) == 3 && !Obj_IsDeleted(obj)){
			ascent(i in 0..5){
				ObjSprite2D_SetSourceRect(obj,i*64,192,(i+1)*64,288);
				wait(6);
			}
			while(Obj_GetValueD(obj,"Anim",0) == 3 && !Obj_IsDeleted(obj)){yield;}
			descent(i in 0..4){
				ObjSprite2D_SetSourceRect(obj,i*64,192,(i+1)*64,288);
				wait(6);
			}
		}
	}
	

	task BlinkTimer{
		while(!Obj_IsDeleted(objBoss)){
			wait(floor(rand(240,480)));
			blink = true;
		}
	}
}

task BossAura_Azure(boss){
	let water = ObjPrim_Create(OBJ_SPRITE_2D);
	ObjPrim_SetTexture(water,GetCurrentScriptDirectory() ~ "./system/azureaura.png");
	ObjSprite2D_SetSourceRect(water,0,0,152,152);
	ObjSprite2D_SetDestCenter(water);
	ObjRender_SetBlendType(water,BLEND_ADD_ARGB);
	ObjRender_SetPosition(water,192,120,0);
	Obj_SetRenderPriority(water,0.3);
	SpinWave(water,0,1);
	let circle = ObjPrim_Create(OBJ_SPRITE_2D);
	ObjPrim_SetTexture(circle,GetCurrentScriptDirectory() ~ "./system/azureaura.png");
	ObjSprite2D_SetSourceRect(circle,152,0,304,152);
	ObjSprite2D_SetDestCenter(circle);
	ObjRender_SetBlendType(circle,BLEND_ADD_ARGB);
	ObjRender_SetPosition(circle,192,120,0);
	Obj_SetRenderPriority(circle,0.3);
	SpinWave(circle,180,-1);

	while(!Obj_IsDeleted(objBoss)){
		let wave = ObjPrim_Create(OBJ_SPRITE_2D);
		ObjPrim_SetTexture(wave,GetCurrentScriptDirectory() ~ "./system/azureaura.png");
		ObjSprite2D_SetSourceRect(wave,304,0,456,152);
		ObjSprite2D_SetDestCenter(wave);
		ObjRender_SetBlendType(wave,BLEND_ADD_ARGB);
		ObjRender_SetPosition(wave,ObjMove_GetX(boss),ObjMove_GetY(boss),0);
		Obj_SetRenderPriority(wave,0.3);
		Expansion(wave);
		wait(3);
	}
	
	task Expansion(obj){
		ascent(i in 0..16){
			let scale = 0.5+i/32;
			ObjRender_SetScaleXYZ(obj,scale,scale,1);
			ObjRender_SetAlpha(obj,i*12);
			yield;
		}
		Obj_Delete(obj);
	}

	task SpinWave(obj,size,dir){
		let angle = rand(0,360);
		let scale;
		while(!Obj_IsDeleted(boss)){
			angle += 5*dir;
			ObjRender_SetAngleZ(obj,angle);
			size += 2;
			scale = sin(size)*0.1+1;
			ObjRender_SetScaleXYZ(obj,scale,scale,1);
			ObjRender_SetPosition(obj,ObjMove_GetX(boss),ObjMove_GetY(boss),1);
			yield;
		}
		Obj_Delete(obj);
	}
}

task TEnd
{
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
	{
		yield;
	}

	DeleteShotAll(TYPE_ALL, TYPE_ITEM);
	Obj_Delete(objBoss);

	loop(30){yield;}

	CloseScript(GetOwnScriptID());
}

function wait(n){loop(n){yield;}}