#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Boss nonspell #1"]
#Text["hi, more rain please"]
#System["script/main/script/system/Gui.dnh"]

#include "script/main/script/lib/Utils.dnh"
#include "script/main/script/lib/Shot.dnh"
#include "script/main/script/lib/Boss.dnh"
#include "script/main/script/lib/SFX.dnh"
#include "script/main/script/lib/Event_Const.dnh"
#include "script/main/img/bullet/shotData_Const.dnh"
#include "script/main/script/stage/1/boss/Anim.txt"

let objBoss;
@Initialize
{
	LoadEnemyShotData("script/main/img/bullet/shotData.dnh");
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	TWork;
	BossResistance(objBoss,120);
	Animation_Azure(objBoss);
	BossAura_Azure(objBoss);
	TEnd;
}

@MainLoop
{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	yield;
}

@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(1150);
	}
	case(EV_REQUEST_TIMER)
	{
		SetScriptResult(30);
	}
}

task TWork
{
	ObjMove_SetDestAtWeight(objBoss,192,140,8,5);
	wait(30);
	let size = 0;
	let shot;
	let angle = rand(0,360);
	let rotation = 2*rand_sign();
	wait(30);
	Obj_SetValue(objBoss,"Anim",0);
	wait(30);
	Obj_SetValue(objBoss,"Anim",3);

	let diff = GetAreaCommonData("GameState","Difficulty",1);
	let diff_speed = [2,3,3,3.5][diff];
	let diff_delay = [4,3,2,2][diff];
	let diff_sway = [0.004,0.006,0.008,0.01][diff];
	

	SizeIncrease;
	IDunnoTryArrowsMaybe;
	AndAlsoRain;

	while(!Obj_IsDeleted(objBoss)){
		ascent(i in 0..5){
			shot = SPShotA1(192+cos(angle+i*72)*size*2,ObjMove_GetY(objBoss)-10+sin(angle+i*72)*size,0,0,RBALL_BLUE,5);
			InstaDelete(shot);
		}
		angle += rotation+rand(0,1);
		yield;
	}

	task AndAlsoRain{
		while(!Obj_IsDeleted(objBoss)){
			ascent(i in 0..5){
				if(Obj_IsDeleted(objBoss)){break;}
				shot = SPShotB2(192+cos(angle+i*72)*size*2,ObjMove_GetY(objBoss)-10+sin(angle+i*72)*size,cos(angle+i*72)*2,-4,0,0.15,0,diff_speed,DROP_BLUE,5);
				wait(diff_delay);
			}
		}
	}

	task SizeIncrease{
		ascent(i in 0..40){
			size = i;
			yield;
		}
		Obj_SetValue(objBoss,"Anim",0);
	}

	task IDunnoTryArrowsMaybe{
		let sway = 0;
		let wind = 0;
		BlueShots;
		while(!Obj_IsDeleted(objBoss)){
			ascent(i in 0..7){
				if(Obj_IsDeleted(objBoss)){break;}
				ascent(j in 0..2){
					j = j*2-1;
					SPShotB2(ObjMove_GetX(objBoss),ObjMove_GetY(objBoss),cos(280+i*5)*2*j,sin(270+i*5)*2,wind*diff_sway,0.05,wind*20,diff_speed,SCALE_CYAN,10);
				}
				wait(3);
			}
			sway += rand(30,35);
			wind = sin(sway);
			if(absolute(wind) < 0.01){yield;}
			wait(14);
		}

		task BlueShots{
			let objPlayer = GetPlayerObjectID();
			while(!Obj_IsDeleted(objBoss)){
				if(ObjMove_GetY(objPlayer) < 100){
					ascent(i in 0..20){
						ascent(j in 0..2){
							j = j*2-1;
							SPShotA1(ObjMove_GetX(objBoss),ObjMove_GetY(objBoss),5,270+i*j*4+rand(0,3),SCALE_BLUE,20);
						}
					}
				}
				wait(40);
			}
		}

	}
}

task InstaDelete(shot){
	ObjShot_SetSpellResist(shot,true);
	ObjRender_SetBlendType(shot,BLEND_ADD_ARGB);
	ObjRender_SetColor(shot,128,128,256);
	wait(6);
	Obj_Delete(shot);
}

task TEnd
{
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
	{
		yield;
	}

	DeleteShotAll(TYPE_ALL, TYPE_ITEM);
	Obj_Delete(objBoss);

	loop(30){yield;}

	CloseScript(GetOwnScriptID());

}
