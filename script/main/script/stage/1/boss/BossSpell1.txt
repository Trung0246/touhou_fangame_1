#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Boss spell #1"]
#Text["Not Instant Not Bodhi"]
#System["script/main/script/system/Gui.dnh"]

#include "script/main/script/lib/Shot.dnh"
#include "script/main/script/lib/Boss.dnh"
#include "script/main/script/lib/SFX.dnh"
#include "script/main/script/lib/Event_Const.dnh"
#include "script/main/img/bullet/shotData_Const.dnh"
#include "script/main/script/stage/1/boss/Anim.txt"
#include "script/main/script/stage/1/boss/SpellLib.txt"

let objBoss;
@Initialize
{
	LoadEnemyShotData("script/main/img/bullet/shotData.dnh");
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	TWork;
	BossResistance(objBoss,180);
	Animation_Azure(objBoss);
	BossAura_Azure(objBoss);
	TEnd;
}

@MainLoop
{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	yield;
}

@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(1950);
	}
	case(EV_REQUEST_TIMER)
	{
		SetScriptResult(40);
	}
}

let shotdown = false;

task TWork
{
	let diff = GetAreaCommonData("GameState","Difficulty",1);
	let diff_startdelay = [10,8,7.5,7.2][diff];
	let diff_middelay = [4,2.5,2,1][diff];
	let diff_speed = [2,2,2.5,2.5][diff];

	ObjMove_SetDestAtWeight(objBoss,192,140,8,5);
	if(ObjMove_GetX(objBoss) > 192){Obj_SetValue(objBoss,"Anim",1);}
	else{Obj_SetValue(objBoss,"Anim",2);}
	wait(30);
	SpellCast(3+diff);
	let n;
	let m;
	let shot;
	let dist = 0;
	let dir = 1;
	let delay;
	let dest;
	wait(30);
	Obj_SetValue(objBoss,"Anim",0);
	wait(90);
	while(!Obj_IsDeleted(objBoss)){
		Obj_SetValue(objBoss,"Anim",3);
		wait(30);
		n = rand(0,120);
		m = rand(5,7)*dir;
		dist = 1;
		delay = diff_startdelay;
		MakeMeSomeBullets;
		ascent(i in 0..200){
			if(Obj_IsDeleted(objBoss)){break;}
			dist += 0.8;
			ascent(j in 0..3){
				shot = SPShotA1(ObjMove_GetX(objBoss)+cos(j*120+n)*dist,ObjMove_GetY(objBoss)+sin(j*120+n)*dist,0,0,RBALL_CYAN,15);
				InstaDelete(shot);
			}
			yield;
			n += m;
		}
		Obj_SetValue(objBoss,"Anim",0);
		delay = diff_middelay;
		descent(i in 0..floor(dist/5)){
			if(Obj_IsDeleted(objBoss)){break;}
			dist -= 5;
			ascent(j in 0..3){
				shot = SPShotA1(ObjMove_GetX(objBoss)+cos(j*120+n)*dist,ObjMove_GetY(objBoss)+sin(j*120+n)*dist,0,0,RBALL_CYAN,15);
				InstaDelete(shot);
			}
			yield;
			n += m;
		}
		dist = 0;
		dir = -dir;
		wait(60);
		dest = ObjMove_GetX(GetPlayerObjectID())+rand(-90,90);
		dest = max(min(dest,282),102);
		if(dest > ObjMove_GetX(objBoss)){Obj_SetValue(objBoss,"Anim",2);}
		else{Obj_SetValue(objBoss,"Anim",1);}
		ObjMove_SetDestAtWeight(objBoss,dest,rand(120,150),5,2);
		wait(30);
		Obj_SetValue(objBoss,"Anim",0);
		wait(60);
	}

	task MakeMeSomeBullets{
		let count = 0;

		while(dist > 0){
			if(Obj_IsDeleted(objBoss)){break;}
			ascent(j in 0..3){
				shot = SPShotA1(ObjMove_GetX(objBoss)+cos(j*120+n)*dist,ObjMove_GetY(objBoss)+sin(j*120+n)*dist,0,j*120+n+150,DROP_CYAN+(count%2)*4,10);
				ObjMove_AddPatternA2(shot,200-dist/5,NO_CHANGE,NO_CHANGE,0.1,0,3);
				ObjMove_AddPatternB2(shot,260-dist/5,NO_CHANGE,NO_CHANGE,0,0.1,0,diff_speed);
				SPShotA2(ObjMove_GetX(objBoss)+cos(j*120+n)*dist,ObjMove_GetY(objBoss)+sin(j*120+n)*dist,0,j*120+n,0.05,2,BALL_BLUE,20);
			}
			count++;
			delay = max(1,delay-0.15);
			wait(ceil(delay));
		}
	}
}

task InstaDelete(shot){
	ObjShot_SetSpellResist(shot,true);
	ObjRender_SetBlendType(shot,BLEND_ADD_ARGB);
	ObjRender_SetColor(shot,128,256,256);
	wait(15);
	Obj_Delete(shot);
}

task TEnd
{
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
	{
		yield;
	}

	DeleteShotAll(TYPE_ALL, TYPE_ITEM);
	NotifyEventAll(EV_BACKGROUND_SPELLBREAK,0);
	Obj_Delete(objBoss);

	loop(30){yield;}

	CloseScript(GetOwnScriptID());
}

function wait(n){loop(n){yield;}}