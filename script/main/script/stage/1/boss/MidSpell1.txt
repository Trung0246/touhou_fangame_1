#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Midboss spell #1"]
#Text["Please stop holding back the development?"]
#System["script/main/script/system/Gui.dnh"]

#include "script/main/script/lib/Shot.dnh"
#include "script/main/script/lib/Boss.dnh"
#include "script/main/script/lib/SFX.dnh"
#include "script/main/script/lib/Event_Const.dnh"
#include "script/main/img/bullet/shotData_Const.dnh"
#include "script/main/script/stage/1/boss/Anim.txt"
#include "script/main/script/stage/1/boss/SpellLib.txt"

let objBoss;
@Initialize
{
	LoadEnemyShotData("script/main/img/bullet/shotData.dnh");
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	TWork;
	BossResistance(objBoss,120);
	Animation_Azure(objBoss);
	BossAura_Azure(objBoss);
	TEnd;
}

@MainLoop
{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	yield;
}

@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(1050);
	}
	case(EV_REQUEST_TIMER)
	{
		SetScriptResult(20);
	}
}

task TWork
{
	let diff = GetAreaCommonData("GameState","Difficulty",2);
	let diff_cyandelay = [12,6][diff-2];
	let shot;
	ObjMove_SetDestAtWeight(objBoss,192,140,8,5);
	if(ObjMove_GetX(objBoss) > 192){Obj_SetValue(objBoss,"Anim",1);}
	else{Obj_SetValue(objBoss,"Anim",2);}
	wait(30);
	SpellCast(diff-1);
	MoreRainForEveryone;
	wait(60);
	Obj_SetValue(objBoss,"Anim",0);
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0){
		ascent(i in 0..12){
			shot = SPShotA2(ObjMove_GetX(objBoss),ObjMove_GetY(objBoss),0.1,GetAngleToPlayer(objBoss)+i*30,0.1,2,BUBBLE_BLUE,20);
			ObjRender_SetBlendType(shot,BLEND_ADD_ARGB);
		}
		wait(80);
	}

	task MoreRainForEveryone{
		let angle = 0;
		let wave = -64;
		let dir = 0;
		let size = 0;
		IncreaseSize;
		RainLaneTime;
		RainNotLaneTime;
		while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0){
			ascent(i in 0..4){
				shot = SPShotA1(192+cos(angle+i*90)*size,absolute(sin(angle+i*90))*10,0,0,RBALL_BLUE,10);
				InstaDelete(shot);
			}
			angle += 6;
			wave++;
			dir = sin(wave)*15;
			yield;
		}

		task IncreaseSize{
			ascent(i in 0..65){
				size = i*3;
				yield;
			}
		}

		task RainLaneTime{
			wait(40);
			while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0){
				ascent(i in 0..4){
					shot = SPShotA2(192+cos(angle+i*90)*size,absolute(sin(angle+i*90))*10,0,90+dir,0.1,2.5,DROP_BLUE,10);
				}
				wait(10);
			}
		}

		task RainNotLaneTime{
			wait(60);
			while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0){
				ascent(i in 0..4){
					shot = SPShotA2(192+cos(angle+i*90)*size,absolute(sin(angle+i*90))*10,0,90+dir,0.1,4,DROP_CYAN,10);
				}
				wait(diff_cyandelay);
			}
		}
	}
}

task InstaDelete(shot){
	ObjShot_SetSpellResist(shot,true);
	ObjRender_SetBlendType(shot,BLEND_ADD_ARGB);
	ObjRender_SetColor(shot,128,256,256);
	wait(9);
	Obj_Delete(shot);
}

task TEnd
{
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
	{
		yield;
	}

	DeleteShotAll(TYPE_ALL, TYPE_ITEM);
	NotifyEventAll(EV_BACKGROUND_SPELLBREAK,0);
	wait(60);
	ObjMove_SetAngle(objBoss,-50);
	ObjMove_SetAcceleration(objBoss,0.1);
	ObjMove_SetMaxSpeed(objBoss,6);
	Obj_SetValue(objBoss,"Anim",2);
	wait(100);
	Obj_Delete(objBoss);
	Obj_Delete(objBoss);

	loop(30){yield;}

	CloseScript(GetOwnScriptID());
}

function wait(n){loop(n){yield;}}