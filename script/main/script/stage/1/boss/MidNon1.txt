#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Boss testing"]
#Text["feat. dangerous ocean fairy"]

#include "script/main/script/lib/Shot.dnh"
#include "script/main/script/lib/Boss.dnh"
#include "script/main/script/lib/SFX.dnh"
#include "script/main/script/lib/Event_Const.dnh"
#include "script/main/img/bullet/shotData_Const.dnh"

let objBoss;
@Initialize
{
	LoadEnemyShotData("script/main/img/bullet/shotData.dnh");
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	TWork;
	BossResistance(objBoss,360);
	Animation_Azure(objBoss);
	BossAura_Azure(objBoss);
	TEnd;
}

@MainLoop
{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	yield;
}

@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(950);
	}
	case(EV_REQUEST_TIMER)
	{
		SetScriptResult(25);
	}
}

let shotdown = false;

task TWork
{
	let wind = 0;
	SetOffTheWind;
	ObjMove_SetPosition(objBoss,192-15,-120);
	Obj_SetValue(objBoss,"Anim",1);
	ObjMove_SetDestAtFrame(objBoss,192-15,140,110);
	wait(110);
	DeleteShotAll(TYPE_SHOT, TYPE_ITEM);
	wait(10);
	Obj_SetValue(objBoss,"Anim",3);
	wait(100);
	Obj_SetValue(objBoss,"Anim",0);
	let angle = rand(0,360);
	let shot;
	let size;
	ActualFiring;
	BossMovement;

	while(!shotdown){
		ascent(i in 0..6){
			shot = SPShotB2(192+cos(angle+i*60)*size*2.5,ObjMove_GetY(objBoss)-10+sin(angle+i*60)*size,1,0,0,0,0,0,RBALL_BLUE,5);
			InstaDelete(shot);
		}
		angle -= 5;
		yield;
	}

	task ActualFiring{
		//Putting this here is probably a sin against good coding practices but you can't stop me
		ascent(i in 0..31){
			size = i*2;
			yield;
		}
		wait(30);
		let diff = GetAreaCommonData("GameState","Difficulty",1);
		let diff_shotdelay = [20,15,12,9][diff];
		while(!shotdown){
			ascent(i in 0..6){
				shot = SPShotB2(192+cos(angle+i*60)*150,ObjMove_GetY(objBoss)+sin(angle+i*60)*60,cos(angle+i*60)*0.5+wind,-4,0,0.1,0,4,DROP_BLUE,10);
			}
			wait(diff_shotdelay+floor(rand(-1,1)));
		}
	}

	task BossMovement{
		let objPlayer = GetPlayerObjectID();
		let dest;
		wait(190);
		while(!shotdown){
			dest = ObjMove_GetX(objPlayer)+rand(-50,50);
			dest = max(min(dest,282),102);
			if(dest > ObjMove_GetX(objBoss)){Obj_SetValue(objBoss,"Anim",2);}
			else{Obj_SetValue(objBoss,"Anim",1);}
			ObjMove_SetDestAtFrame(objBoss,dest,140,50);

			PlaySFX(SFX_SHOT1);
			ascent(i in -5..6){
				ascent(j in 0..2){
					shot = SPShotA1(ObjMove_GetX(objBoss),ObjMove_GetY(objBoss),5,235+i*2.5+j*70,SCALE_CYAN,10);
					ObjMove_AddPatternB2(shot,10,NO_CHANGE,NO_CHANGE,0,0.1,0,4);
				}
			}
			
			wait(50);
			Obj_SetValue(objBoss,"Anim",0);
			wait(190);
		}
		
	}

	task SetOffTheWind{
		let n = 0;
		while(!Obj_IsDeleted(objBoss)){
			n += 2;
			wind = sin(n);
			yield;
		}
	}
}

task InstaDelete(shot){
	ObjShot_SetSpellResist(shot,true);
	ObjRender_SetBlendType(shot,BLEND_ADD_ARGB);
	ObjRender_SetColor(shot,128,128,256);
	wait(5);
	Obj_Delete(shot);
}

task Animation_Azure(obj){
	let blink = false;
	BlinkTimer;
	ObjSprite2D_SetDestRect(obj,-32,-48,32,48);
	ObjPrim_SetTexture(obj,"script/main/img/enemy/boss/1/azuresprite.png");
	while(!Obj_IsDeleted(obj)){
		while(Obj_GetValueD(obj,"Anim",0) == 0 && !Obj_IsDeleted(obj)){
			if(blink){
				blink = false;
				ObjSprite2D_SetSourceRect(obj,256,0,320,96);
				wait(6);
				ObjSprite2D_SetSourceRect(obj,0,0,64,96);
				wait(6);
			}
			else{
				ObjSprite2D_SetSourceRect(obj,0,0,64,96);
				wait(12);
			}
			ascent(i in 1..4){
				if(Obj_GetValueD(obj,"Anim",0) != 0 || Obj_IsDeleted(obj)){break;}
				ObjSprite2D_SetSourceRect(obj,i*64,0,(i+1)*64,96);
				wait(12);
			}
		}
		if(Obj_GetValueD(obj,"Anim",0) == 1){
			ObjRender_SetScaleX(obj,-1);
			Obj_SetValue(obj,"Anim",2);
		}
		if(Obj_GetValueD(obj,"Anim",0) == 2 && !Obj_IsDeleted(obj)){
			ascent(i in 0..5){
				ObjSprite2D_SetSourceRect(obj,i*64,96,(i+1)*64,192);
				wait(6);
			}
			while(Obj_GetValueD(obj,"Anim",0) == 2 && !Obj_IsDeleted(obj)){
				yield;
			}
			descent(i in 0..4){
				ObjSprite2D_SetSourceRect(obj,i*64,96,(i+1)*64,192);
				wait(6);
			}
			ObjRender_SetScaleX(obj,1);
		}
		if(Obj_GetValueD(obj,"Anim",0) == 3 && !Obj_IsDeleted(obj)){
			ascent(i in 0..5){
				ObjSprite2D_SetSourceRect(obj,i*64,192,(i+1)*64,288);
				wait(6);
			}
			while(Obj_GetValueD(obj,"Anim",0) == 3 && !Obj_IsDeleted(obj)){yield;}
			descent(i in 0..4){
				ObjSprite2D_SetSourceRect(obj,i*64,192,(i+1)*64,288);
				wait(6);
			}
		}
	}
	

	task BlinkTimer{
		while(!Obj_IsDeleted(objBoss)){
			wait(floor(rand(240,480)));
			blink = true;
		}
	}
}

task BossAura_Azure(boss){
	let water = ObjPrim_Create(OBJ_SPRITE_2D);
	ObjPrim_SetTexture(water,"script/main/img/enemy/boss/1/azureaura.png");
	ObjSprite2D_SetSourceRect(water,0,0,152,152);
	ObjSprite2D_SetDestCenter(water);
	ObjRender_SetBlendType(water,BLEND_ADD_ARGB);
	ObjRender_SetPosition(water,192,120,0);
	Obj_SetRenderPriority(water,0.3);
	ObjRender_SetAlpha(water,192);
	SpinWave(water,0,1);
	let circle = ObjPrim_Create(OBJ_SPRITE_2D);
	ObjPrim_SetTexture(circle,"script/main/img/enemy/boss/1/azureaura.png");
	ObjSprite2D_SetSourceRect(circle,152,0,304,152);
	ObjSprite2D_SetDestCenter(circle);
	ObjRender_SetBlendType(circle,BLEND_ADD_ARGB);
	ObjRender_SetPosition(circle,192,120,0);
	Obj_SetRenderPriority(circle,0.3);
	ObjRender_SetAlpha(circle,192);
	SpinWave(circle,180,-1);

	while(!Obj_IsDeleted(objBoss)){
		let wave = ObjPrim_Create(OBJ_SPRITE_2D);
		ObjPrim_SetTexture(wave,"script/main/img/enemy/boss/1/azureaura.png");
		ObjSprite2D_SetSourceRect(wave,304,0,456,152);
		ObjSprite2D_SetDestCenter(wave);
		ObjRender_SetBlendType(wave,BLEND_ADD_ARGB);
		ObjRender_SetPosition(wave,ObjMove_GetX(boss),ObjMove_GetY(boss),0);
		Obj_SetRenderPriority(wave,0.3);
		Expansion(wave);
		wait(3);
	}
	
	task Expansion(obj){
		ascent(i in 0..16){
			let scale = 0.5+i/32;
			ObjRender_SetScaleXYZ(obj,scale,scale,1);
			ObjRender_SetAlpha(obj,i*12);
			yield;
		}
		Obj_Delete(obj);
	}

	task SpinWave(obj,size,dir){
		let angle = rand(0,360);
		let scale;
		while(!Obj_IsDeleted(objBoss)){
			angle += 5*dir;
			ObjRender_SetAngleZ(obj,angle);
			size += 2;
			scale = sin(size)*0.1+1;
			ObjRender_SetScaleXYZ(obj,scale,scale,1);
			ObjRender_SetPosition(obj,ObjMove_GetX(boss),ObjMove_GetY(boss),1);
			yield;
		}
		Obj_Delete(obj);
	}
}

task TEnd
{
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
	{
		yield;
	}

	DeleteShotAll(TYPE_SHOT, TYPE_ITEM);
	shotdown = true;
	wait(60);
	ObjMove_SetAngle(objBoss,-50);
	ObjMove_SetAcceleration(objBoss,0.1);
	ObjMove_SetMaxSpeed(objBoss,6);
	Obj_SetValue(objBoss,"Anim",2);
	wait(100);
	Obj_Delete(objBoss);

	loop(30){yield;}

	CloseScript(GetOwnScriptID());
}

function wait(n){loop(n){yield;}}