#TouhouDanmakufu[Stage]
#ScriptVersion[3]
#Title["Stage test"]
#Text["Stage text"]
#System["script/main/script/system/Gui.dnh"]
#Player["script/main/script/player/reimu/Reimu.dnh"]
#Background["script/main/script/stage/1/background/Background.dnh"]

//#include "script/main/script/system/Init.dnh"

#include "script/main/script/lib/Utils.dnh"
#include "script/main/script/lib/Stage.dnh"
#include "script/main/script/lib/Event_Const.dnh"
#include "script/main/script/lib/Raindrop.dnh"
#include "script/main/script/lib/Shot.dnh"
#include "script/main/script/lib/SFX.dnh"
#include "script/main/script/stage/1/Enemy.dnh"
#include "script/main/img/bullet/shotData_Const.dnh"

@Initialize {
	Stage_Init;
	Stage_Main();
}

@MainLoop {
	yield;
}
 
@Finalize {
	
}

@Event{
	alternative(GetEventType())
	case(EV_ENEMY_SHOOTDOWN){ //Argument: [enemy type, x, y]
		let arg = GetEventArgument(0);
		let obj;
		alternative(arg[0])
		case(FAIRY_STREAM){
			loop(5){
				obj = CreateItemU1(7,arg[1]+rand(-20,20),arg[2]+rand(-20,20),0);
				ObjItem_SetRenderScoreEnable(obj, false);
				obj = CreateItemU1(9,arg[1]+rand(-20,20),arg[2]+rand(-20,20),0);
				ObjItem_SetRenderScoreEnable(obj, false);
			}
			let dir = (floor(arg[1]/192)%2)*2-1;
			ascent(i in -1..2){
				CreateShotRaindrop(arg[1],arg[2],(-3-i/2)*dir,-2-i,0.05*dir,0.05,0,2,RAINDROP_RED+absolute(i),0);
			}
		}
		case(FAIRY_RAINCIRCLE){
			loop(2){
				obj = CreateItemU1(7,arg[1]+rand(-20,20),arg[2]+rand(-20,20),0);
				ObjItem_SetRenderScoreEnable(obj, false);
				obj = CreateItemU1(9,arg[1]+rand(-20,20),arg[2]+rand(-20,20),0);
				ObjItem_SetRenderScoreEnable(obj, false);
			}
			ascent(i in 0..2){
				let dir = i*2-1;
				CreateShotRaindrop(arg[1],arg[2],dir/2,-3,0.01*-dir,0.05,0.1*dir,2,RAINDROP_GREEN,0);
			}
		}
	}
}

task Stage_Main() {
	//Init boss scripts
	let midboss = LoadScriptInThread(GetCurrentScriptDirectory() ~ "./boss/Plural.dnh");

	SetCommonData("StageWaitTime",0);
	//Stage content
	wait(60);
	NotifyEventAll(EV_BGM_PLAY,2);
	//Opening wave
	ascent(i in 0..3){
		ascent(j in 0..2){
			StageEnemy(100+j*184,-50,FAIRY_STREAM,16,12,100,[284-j*184,100+i*32,i%2+2]);
		}
		stageWait(10);
	}
	stageWait(360);
	StageTitle(1);
	stageWait(300);
	//Fairy lane wave
	ascent(i in -1..2){
		StageEnemy(192+i*40,-100+absolute(i)*10,FAIRY_RAINCIRCLE,16,12,55,[192-i*120,180-absolute(i)*10]);
	}
	stageWait(180);
	ascent(i in -2..3){
		StageEnemy(192+i*40,-100+absolute(i)*10,FAIRY_RAINCIRCLE,16,12,55,[192-i*80,140-absolute(i)*10]);
	}
	stageWait(180);
	ascent(i in -3..4){
		StageEnemy(192+i*40,-100+absolute(i)*10,FAIRY_RAINCIRCLE,16,12,55,[192-i*60,100-absolute(i)*10]);
	}
	stageWait(180);
	ascent(i in -4..5){
		StageEnemy(192+i*40,-100+absolute(i)*10,FAIRY_RAINCIRCLE,16,12,55,[192-i*40,60-absolute(i)*10]);
	}
	wait(600);

	StartScript(midboss);
	CheckBossActive(midboss);
	while(!IsCloseScript(midboss)){yield;}

	//CloseStgScene();
}
