#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Boss nonspell #1"]
#Text["That's a pretty good intimidation roll"]
#System["script/main/script/system/Gui.dnh"]

#include "script/main/script/lib/Utils.dnh"
#include "script/main/script/lib/Shot.dnh"
#include "script/main/script/lib/Boss.dnh"
#include "script/main/script/lib/SFX.dnh"
#include "script/main/script/lib/Event_Const.dnh"
#include "script/main/img/bullet/shotData_Const.dnh"
#include "script/main/script/stage/2/boss/Anim.txt"
#include "script/main/script/stage/2/boss/BossCommon.txt"
#include "script/main/script/stage/1/boss/Anim.txt"

let objBoss;
@Initialize
{
	LoadEnemyShotData("script/main/img/bullet/shotData.dnh");
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	TWork;
	BossResistance(objBoss,300);
	Animation_Azure(objBoss);
	BossAura_Sakkagumi(objBoss);
	TEnd;
}

@MainLoop
{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	yield;
}

@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(1850);
	}
	case(EV_REQUEST_TIMER)
	{
		SetScriptResult(40);
	}
}

task TWork
{
	ObjMove_SetDestAtWeight(objBoss,192,150,8,5);
	let diff = GetAreaCommonData("GameState","Difficulty",1);
	let diff_mainspawn = [5,3,3,2][diff];
	let diff_spamspawn = [6,4,3,3][diff];
	let diff_curvetime = [70,90,110,150][diff];
	wait(120);
	let dir = rand_sign();
	let angle;
	let shot;
	while(!Obj_IsDeleted(objBoss)){
		angle = GetAngleToPlayer(objBoss)+45;
		ascent(i in 0..4){
			shot = SPShotA2(ObjMove_GetX(objBoss),ObjMove_GetY(objBoss),0,angle+i*90,0.1,6,RBALL_MAGENTA,0);
			ObjMove_SetAngularVelocity(shot,1.5*dir);
			ObjMove_AddPatternA2(shot,diff_curvetime,NO_CHANGE,NO_CHANGE,0,0,0);
			CloudBullet(shot);
		}
		dir = -dir;
		wait(360);
	}

	task CloudBullet(obj){
		ObjShot_SetSpellResist(obj,true);
		yield;
		GetMeSomeDanmaku;
		PlusSomeSpam;
		ObjShot_SetIntersectionEnable(obj,false);
		ObjRender_SetAlpha(obj,0);
		while(!Obj_IsDeleted(obj)){
			SakkaCloud(ObjMove_GetX(obj),ObjMove_GetY(obj),70,1.0,75);
			wait(4);
		}

		task GetMeSomeDanmaku{
			let wave = rand(0,360);
			let speed;
			while(!Obj_IsDeleted(obj)){
				wave += 38;
				speed = sin(wave)/4;
				UltraDelayBullet(ObjMove_GetX(obj),ObjMove_GetY(obj),1.2+speed,ObjMove_GetAngle(obj)+50,RBALL_MAGENTA,30,102,0,0,0);
				UltraDelayBullet(ObjMove_GetX(obj),ObjMove_GetY(obj),1.2+speed,ObjMove_GetAngle(obj)-50,RBALL_MAGENTA,30,102,0,0,0);
				wait(diff_mainspawn);
			}
		}

		task PlusSomeSpam{
			let a;
			while(!Obj_IsDeleted(obj)){
				a = GetPointAngle(ObjMove_GetX(obj),ObjMove_GetY(obj),ObjMove_GetX(objBoss),ObjMove_GetY(objBoss));
				ascent(i in 0..2){
					UltraDelayBullet(ObjMove_GetX(obj)+rand(-40,40),ObjMove_GetY(obj)+rand(-40,40),1.2,a+i*180,BALL_MAGENTA,30,102,0,0,0);
				}
				wait(diff_spamspawn);
			}
		}
	}
}

task TEnd
{
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
	{
		yield;
	}

	DeleteShotAll(TYPE_ALL, TYPE_ITEM);
	Obj_Delete(objBoss);

	loop(30){yield;}

	CloseScript(GetOwnScriptID());

}
