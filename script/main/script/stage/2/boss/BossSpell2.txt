#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Boss spell #2"]
#Text["Lovely, tasty pink fire! <3"]
#System["script/main/script/system/Gui.dnh"]

#include "script/main/script/lib/Utils.dnh"
#include "script/main/script/lib/Shot.dnh"
#include "script/main/script/lib/Boss.dnh"
#include "script/main/script/lib/SFX.dnh"
#include "script/main/script/lib/Event_Const.dnh"
#include "script/main/img/bullet/shotData_Const.dnh"
#include "script/main/script/stage/2/boss/Anim.txt"
#include "script/main/script/stage/2/boss/BossCommon.txt"
#include "script/main/script/stage/1/boss/Anim.txt"
#include "script/main/script/lib/Spellcard.dnh"

let objBoss;
@Initialize
{
	LoadEnemyShotData("script/main/img/bullet/shotData.dnh");
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	TWork;
	BossResistance(objBoss,300);
	Animation_Azure(objBoss);
	BossAura_Sakkagumi(objBoss);
	TEnd;
}

@MainLoop
{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	yield;
}

@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(2250);
	}
	case(EV_REQUEST_TIMER)
	{
		SetScriptResult(40);
	}
}

task TWork
{
	ObjMove_SetDestAtWeight(objBoss,192,120,8,5);
	let diff = GetAreaCommonData("GameState","Difficulty",1);
	wait(30);
	SpellCast(19+diff);
	let diff_space = [40,35,30,30][diff];
	let diff_delay = [100,70,50,45][diff];
	let diff_spin = [40,20,20,20][diff];
	let diff_speed = [1.2,1.2,1.4,1.6][diff];
	let diff_count = [12,15,18,20][diff];
	wait(120);
	let n;
	let m;
	let shot;
	let dir = rand_sign();
	let objPlayer = GetPlayerObjectID();
	SpiralAway;
	while(!Obj_IsDeleted(objBoss)){
		CloudCircle(max(100,min(284,ObjMove_GetX(objPlayer))),max(100,min(348,ObjMove_GetY(objPlayer))),3,diff_space);
		CloudCircle(ObjMove_GetX(objBoss),ObjMove_GetY(objBoss),6,20);
		dir = -dir;
		wait(300);
	}

	task SpiralAway{
		let a = rand(0,360);
		let d = rand_sign();
		let pos;
		while(!Obj_IsDeleted(objBoss)){
			a += diff_spin*d;
			ascent(i in 0..12){
				pos = [ObjMove_GetX(objBoss)+cos(i*30+a)*60,ObjMove_GetY(objBoss)+sin(i*30+a)*60];
				SakkaCloud(pos[0],pos[1],50,0.5);
				loop(4){
					UltraDelayBullet(pos[0]+rand(-40,40),pos[1]+rand(-40,40),diff_speed,i*30+a,DOT_MAGENTA,20,72,0,0,0);
				}
				UltraDelayBullet(pos[0],pos[1],diff_speed,i*30+a,BBALL_MAGENTA,10,72,0,0,0);
			}
			wait(diff_delay);
		}
	}

	task CloudCircle(x,y,count,shift){
		let spin = 360/diff_count;
		let start = rand(0,360);
		ascent(i in 0..diff_count){
			SakkaCloud(x+cos(i*spin*-dir+start)*100,y+sin(i*spin*-dir+start)*100,20+i*2,0.7);
		}
		wait(52);
		if(Obj_IsDeleted(objBoss)){return;}
		ascent(i in 0..diff_count){
			ascent(j in 0..5){
				shot = SPShotA2(x+cos(i*spin*dir+start)*100,y+sin(i*spin*dir+start)*100,0,i*spin*dir+180+shift*dir+rand(-3,3)+start,0.02,1+j/2,FIRE_MAGENTA,20);
				ObjRender_SetBlendType(shot,BLEND_ADD_ARGB);
			}
			wait(2);
		}
	}
}


task TEnd
{
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
	{
		yield;
	}

	DeleteShotAll(TYPE_ALL, TYPE_ITEM);
	Obj_Delete(objBoss);

	loop(30){yield;}

	CloseScript(GetOwnScriptID());

}
