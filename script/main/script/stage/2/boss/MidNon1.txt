#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Midboss nonspell #1"]
#Text["Try to, uh, not overcomplicate it, for once"]
#System["script/main/script/system/Gui.dnh"]

#include "script/main/script/lib/Utils.dnh"
#include "script/main/script/lib/Shot.dnh"
#include "script/main/script/lib/Boss.dnh"
#include "script/main/script/lib/SFX.dnh"
#include "script/main/script/lib/Event_Const.dnh"
#include "script/main/img/bullet/shotData_Const.dnh"
#include "script/main/script/stage/2/boss/AnimJ.txt"

let objBoss;
@Initialize
{
	LoadEnemyShotData("script/main/img/bullet/shotData.dnh");
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	TWork;
	BossResistance(objBoss,300);
	Animation_Jiriri(objBoss);
	BossAura_Jiriri(objBoss);
	TEnd;
}

@MainLoop
{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	yield;
}

@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(1850);
	}
	case(EV_REQUEST_TIMER)
	{
		SetScriptResult(40);
	}
}

task TWork
{
	let diff = GetAreaCommonData("GameState","Difficulty",1);
	let diff_maincount = [16,24,24,24][diff];
	let diff_splitcount = [4,6,8,9][diff];
	let diff_height = [170,180,200,200][diff];
	ObjMove_SetDestAtWeight(objBoss,192,diff_height,8,5);
	wait(120);
	//UltraCloud;
	let angle;
	let shot;
	let n = 0;
	let m;
	let split = 360/diff_maincount;
	let split2 = 360/diff_splitcount;
	MoveBoss;
	while(!Obj_IsDeleted(objBoss)){
		m = rand(0,360);
		ascent(i in 0..diff_maincount){
			i *= split;
			SPShotA2(ObjMove_GetX(objBoss)+cos(i+m)*40,ObjMove_GetY(objBoss)+sin(i+m)*40,0,i+m,0.04,1.8,RICE_GREY,30);
			ascent(j in 0..4){
				SPShotA2(ObjMove_GetX(objBoss)+cos(j*90+n)*40,ObjMove_GetY(objBoss)+sin(j*90+n)*40,0,i+m,0.04,1.8,RICE_GREY,30);
			}
		}
		ascent(i in 0..4){
			shot = SPShotA2(ObjMove_GetX(objBoss)+cos(i*90+n)*30,ObjMove_GetY(objBoss)+sin(i*90+n)*30,0,i*90+n,0.04,1.8,ORB_ORANGE,10);
			LaserBomb(shot,m);
		}
		n += 20;
		wait(180);
	}

	task LaserBomb(obj,a){
		wait(60);
		if(Obj_IsDeleted(obj)){return;}
		ObjMove_SetSpeed(obj,0);
		ascent(i in 0..diff_splitcount){
			i *= split2;
			SPShotA2(ObjMove_GetX(obj)+cos(i+a)*40,ObjMove_GetY(obj)+sin(i+a)*40,0,i+a,0.04,2,RICE_YELLOW,30);
			ascent(j in 0..4){
				SPShotA2(ObjMove_GetX(obj)+cos(j*90)*40,ObjMove_GetY(obj)+sin(j*90)*40,0,i+a,0.04,2,RICE_YELLOW,30);
			}
		}
		ObjShot_SetDeleteFrame(obj,0);
	}


	task MoveBoss{
		wait(180);
		let dir = rand_sign();
		let time;
		ObjMove_SetAngle(objBoss,0);
		while(!Obj_IsDeleted(objBoss)){
			ObjMove_SetAcceleration(objBoss,0.05*dir);
			ObjMove_SetMaxSpeed(objBoss,0.7*dir);
			Obj_SetValue(objBoss,"Anim",0);
			yield;
			Obj_SetValue(objBoss,"Anim",1+(dir+1)/2);
			wait(120);
			loop(floor(rand(60,180))){
				if(Obj_IsDeleted(objBoss) || ObjMove_GetX(objBoss) < 100 || ObjMove_GetX(objBoss) > 284){break;}
				yield;	
			}
			dir = -dir;
		}
	}

	task UltraCloud{
		let a;
		while(!Obj_IsDeleted(objBoss)){
			a = rand(0,180);
			JiririCloud(192+cos(a)*200,sin(a)*rand(10,200),a+90+rand(-10,10),floor(rand(30,90)));
			wait(2);
		}
	}
}

task JiririCloud(x,y,angle,duration){
	let type = floor(rand(2,4));
	let cloud = ObjPrim_Create(OBJ_SPRITE_2D);
	ObjPrim_SetTexture(cloud,"script/main/img/stage/2/Cloud" ~ itoa(type)  ~ ".png");
	ObjSprite2D_SetSourceRect(cloud,0,0,200,200);
	ObjSprite2D_SetDestCenter(cloud);
	ObjRender_SetColor(cloud,128,128,128);
	Obj_SetRenderPriority(cloud,0.6);
	ObjRender_SetPosition(cloud,x,y,0);
	MoveCloud;
	ascent(i in 0..33){
		if(Obj_IsDeleted(objBoss)){break;}
		ObjRender_SetAlpha(cloud,i*8);
		yield;
	}

	loop(duration){
		if(Obj_IsDeleted(objBoss)){break;}
		yield;
	}

	descent(i in 0..16){
		ObjRender_SetAlpha(cloud,i*16);
		yield;
	}
	Obj_Delete(cloud);

	task MoveCloud{
		let move = [cos(angle)/2,sin(angle)/2];
		while(!Obj_IsDeleted(cloud)){
			ObjRender_SetPosition(cloud,ObjRender_GetX(cloud)+move[0],ObjRender_GetY(cloud)+move[1],0);
			yield;
		}
	}
}

task TEnd
{
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
	{
		yield;
	}

	DeleteShotAll(TYPE_ALL, TYPE_ITEM);
	Obj_Delete(objBoss);

	loop(30){yield;}

	CloseScript(GetOwnScriptID());

}
