#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Last Word of Jiriri"]
#Text["Always keep a cloud youkai between you and the sun"]
#System["script/main/script/system/Gui.dnh"]

#include "script/main/script/lib/Utils.dnh"
#include "script/main/script/lib/Shot.dnh"
#include "script/main/script/lib/Boss.dnh"
#include "script/main/script/lib/SFX.dnh"
#include "script/main/script/lib/Event_Const.dnh"
#include "script/main/img/bullet/shotData_Const.dnh"
#include "script/main/script/stage/2/boss/AnimJ.txt"
#include "script/main/script/lib/Spellcard.dnh"

let objBoss;
@Initialize
{
	LoadEnemyShotData("script/main/img/bullet/shotData.dnh");
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	TWork;
	BossResistance(objBoss,300);
	Animation_Jiriri(objBoss);
	BossAura_Jiriri(objBoss);
	TEnd;
}

@MainLoop
{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	yield;
}

@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(2450);
	}
	case(EV_REQUEST_TIMER)
	{
		SetScriptResult(60);
	}
}


let shotdown = false;
task TWork
{
	let diff = GetAreaCommonData("GameState","Difficulty",1);
	ObjMove_SetPosition(objBoss,-100,-100);
	UltraCloud;
	wait(30);
	SpellCast(28);
	wait(30);
	ObjMove_SetPosition(objBoss,192,90);
	ObjMove_SetDestAtWeight(objBoss,192,250,8,5);
	wait(60);
	let dir = rand_sign();
	let shot;
	let n;
	while(!shotdown){
		Obj_SetValue(objBoss,"Anim",3);
		wait(30);
		ascent(i in 1..31){
			if(shotdown){break;}
			PlaySFX(SFX_LASER);
			ascent(j in 0..2){
				j = j*2-1;
				SPStraightLaser(ObjMove_GetX(objBoss),i*5,90+i*11*j+10*j,512,8,60,SLASER_YELLOW+i%3,0);
				SPShotA1(ObjMove_GetX(objBoss),i*5,6,90+i*11*j+5*j,ORB_YELLOW+i%2,20);
				ascent(k in -2..3){
					shot = SPShotA2(ObjMove_GetX(objBoss),i*5,1,90+i*10*j+12*j+k*20,0.02,4,RBALL_ORANGE,20);
					ObjRender_SetBlendType(shot,BLEND_ADD_ARGB);
				}
			}
			yield;
		}
		Obj_SetValue(objBoss,"Anim",0);
		wait(120);
		yield;
		Obj_SetValue(objBoss,"Anim",1+(dir+1)/2);
		n = rand(0,15);
		ObjMove_SetDestAtFrame(objBoss,192+rand(50,100)*dir,250,80);
		RegularShots;
		wait(50);
		RegularShots;
		dir = -dir;
	}

	task RegularShots{
		ascent(j in 0..3){
			if(shotdown){break;}
			PlaySFX(SFX_SHOT1);
			ascent(i in 0..24){
				ascent(k in 0..3){
					SPShotA2(ObjMove_GetX(objBoss),ObjMove_GetY(objBoss),1,i*15+n,0.1,3-j/2-k*0.9,RBALL_GREY-j*8,10);
				}
			}
			wait(5);
		}
	}

	task UltraCloud{
		let a;
		loop(30){
			a = rand(0,180);
			JiririCloud(192+cos(a)*200,sin(a)*rand(30,180),a+90+rand(-10,10),floor(rand(40,90)));
			yield;
		}
		while(!shotdown){
			a = rand(0,180);
			JiririCloud(192+cos(a)*200,sin(a)*rand(30,180),a+90+rand(-10,10),floor(rand(40,90)));
			wait(2);
		}
	}
}

task JiririCloud(x,y,angle,duration){
	let type = floor(rand(2,4));
	let cloud = ObjPrim_Create(OBJ_SPRITE_2D);
	ObjPrim_SetTexture(cloud,"script/main/img/stage/2/Cloud" ~ itoa(type)  ~ ".png");
	ObjSprite2D_SetSourceRect(cloud,0,0,200,200);
	ObjSprite2D_SetDestCenter(cloud);
	ObjRender_SetColor(cloud,128,128,128);
	Obj_SetRenderPriority(cloud,0.75);
	ObjRender_SetPosition(cloud,x,y,0);
	MoveCloud;
	ascent(i in 0..33){
		if(Obj_IsDeleted(objBoss)){break;}
		ObjRender_SetAlpha(cloud,i*8);
		yield;
	}

	loop(duration){
		if(Obj_IsDeleted(objBoss)){break;}
		yield;
	}

	descent(i in 0..16){
		ObjRender_SetAlpha(cloud,i*16);
		yield;
	}
	Obj_Delete(cloud);

	task MoveCloud{
		let move = [cos(angle)/2,sin(angle)/2];
		while(!Obj_IsDeleted(cloud)){
			ObjRender_SetPosition(cloud,ObjRender_GetX(cloud)+move[0],ObjRender_GetY(cloud)+move[1],0);
			yield;
		}
	}
}

task TEnd
{
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0)
	{
		yield;
	}

	DramaticBossDeath(objBoss);
	shotdown = true;
	wait(60);
	//NotifyEventAll(EV_BACKGROUND_SPELLBREAK,0);
	wait(120);

	CloseScript(GetOwnScriptID());

}
