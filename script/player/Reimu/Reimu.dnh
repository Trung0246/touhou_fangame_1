#TouhouDanmakufu[Player]
#ScriptVersion[3]
#ID["Reimu"]
#Title["Reimu Hakurei"]
#Text["Unfocused: Homing [r] Focused: Needles"]
#ReplayName["Reimu"]

#include "./lib/Event.dnh"

let PlayerID = GetPlayerObjectID();
let objPlayer = ObjPrim_Create(OBJ_SPRITE_2D);
let objHitbox = ObjPrim_Create(OBJ_SPRITE_2D);

let isFocus = false;

let CSD = GetCurrentScriptDirectory();

let shot = CSD ~ "./lib/ReimuShot.dnh";
let sprite = CSD ~ "./img/sprite.png";
let hitbox = CSD ~ "./img/hitbox.png";

let deathSE = CSD ~ "./se/deathSE.wav";

let hitboxAlpha;

let count = -1;

@Initialize {
	LoadPlayerShotData(shot); // Load the shots
	LoadTexture(sprite); // Load the sprites

	ObjPlayer_AddIntersectionCircleA1(PlayerID, 0, 0, 1, 24); // Set hitbox

	SetPlayerSpeed(4.5, 2.0);

	SetPlayerClip(0+6, 0+24, 384-6, 448-16);

	SetPlayerDownStateFrame(60);
	SetPlayerRebirthFrame(30); // Deathbomb window

	SetPlayerAutoItemCollectLine(120); //POC line

	LoadSound(deathSE);

	TRenderPlayer;
	TRenderHitbox(GetPlayerX,GetPlayerY);
}

@Event {
	alternative(GetEventType)

	case(EV_REQUEST_SPELL){ // Bomb stuff (not implemented)
		if(GetPlayerSpell > 0){
			SetScriptResult(true);
		}
		else{
			SetScriptResult(false);
		}
	}

	case(EV_PLAYER_SHOOTDOWN){ // Death stuff
		PlaySE(deathSE);
		SetForbidPlayerShot(true);
		SetForbidPlayerSpell(true);
		ObjRender_SetAlpha(objPlayer,0);
	}

	case(EV_PLAYER_REBIRTH){ // Upon revival
		SetForbidPlayerShot(false);
		SetForbidPlayerSpell(false);
		ObjRender_SetAlpha(objPlayer,255);
		SetPlayerInvincibilityFrame(120);
		SetPlayerSpell(3);
	}
}

@MainLoop {
	if(GetVirtualKeyState(VK_SLOWMOVE) == KEY_PUSH || GetVirtualKeyState(VK_SLOWMOVE) == KEY_HOLD){
		isFocus = true;
	}
	if(GetVirtualKeyState(VK_SLOWMOVE) == KEY_PULL){
		isFocus = false;
	}

	if(IsPermitPlayerShot){ // Lets the player shoot stuff
		if(GetVirtualKeyState(VK_SHOT) == KEY_PUSH || GetVirtualKeyState(VK_SHOT) == KEY_HOLD){
			count++;
			if(count >= 5){
				count = 0;
			}
		}
		if(GetVirtualKeyState(VK_SHOT) == KEY_PULL){
			count = -1;
		}

		if(isFocus){
			if(count == 3){
				// fire the bullets
				CreatePlayerShotA1(GetPlayerX-8,GetPlayerY-20,3,270,5.25,1,1);
				CreatePlayerShotA1(GetPlayerX+8,GetPlayerY-20,3,270,5.25,1,1);
			}
		}
		else {
			if(count == 3){
				// fire the bullets
				CreatePlayerShotA1(GetPlayerX-8,GetPlayerY-20,3,270,5,1,1);
				CreatePlayerShotA1(GetPlayerX+8,GetPlayerY-20,3,270,5,1,1);
			}
		}	
	}

	yield;
}

function wait(n){
	loop(n){yield;}
}

@Finalize {

}

task TRenderPlayer {
	// Basic stuff
	ObjPrim_SetTexture(objPlayer,sprite);
	Obj_SetRenderPriorityI(objPlayer,31);
	ObjRender_SetBlendType(objPlayer,BLEND_ALPHA);
	ObjRender_SetAngleXYZ(objPlayer,0,0,0);
	ObjRender_SetScaleXYZ(objPlayer,1,1,0);
	ObjRender_SetAlpha(objPlayer,255);
	ObjSprite2D_SetSourceRect(objPlayer,0,0,32,48);
	ObjSprite2D_SetDestCenter(objPlayer);
	ObjRender_SetPosition(objPlayer,GetPlayerX,GetPlayerY,0);
	TUpdatePosition; //I'm retarded so now this is a thing
	
	while(!Obj_IsDeleted(objPlayer)){

		if (GetVirtualKeyState(VK_LEFT) == 3) { // if pressing left
			ascent(i in 0..7){
				ObjSprite2D_SetSourceRect(objPlayer,0+(32*i),48,32+(32*i),96);
				loop (10) {
					if (GetVirtualKeyState(VK_LEFT) != 3) {
						break;
					}
					yield;
				}
			}
			ascent(i in 5..7){
				ObjSprite2D_SetSourceRect(objPlayer,0+(32*i),48,32+(32*i),96);
				loop (10) {
					if (GetVirtualKeyState(VK_LEFT) != 3) {
						break;
					}
					yield;
				}
			}
		} 

		else if (GetVirtualKeyState(VK_RIGHT) == 3) { // if pressing right
			ascent(i in 0..7){
				ObjSprite2D_SetSourceRect(objPlayer,0+(32*i),96,32+(32*i),144);
				loop (10) {
					if (GetVirtualKeyState(VK_RIGHT) != 3) {
						break;
					}
					yield;
				}
			}
			ascent(i in 5..7){ 
				ObjSprite2D_SetSourceRect(objPlayer,0+(32*i),96,32+(32*i),144);
				loop (10) {
					if (GetVirtualKeyState(VK_RIGHT) != 3) {
						break;
					}
					yield;
				}
			}
		} 

		else { // if pressing up/down or nothing
			ascent (i in 0..7) {
				ObjSprite2D_SetSourceRect(objPlayer,0+(32*i),0,32+(32*i),48);
				loop (10) {
					if (GetVirtualKeyState(VK_LEFT) == 3 || GetVirtualKeyState(VK_RIGHT) == 3) {
						break;
					}
					yield;
				}
			}
		}

		yield;
	}
}

task TUpdatePosition{
	while(!Obj_IsDeleted(objPlayer)){
		ObjRender_SetPosition(objPlayer,GetPlayerX,GetPlayerY,0); // Makes the sprite actually be where the player is\
		yield;
	}
}

task TRenderHitbox(x,y){
	let hitboxAlpha = 0;
	let angleS = 0;

	// More basic stuff
	ObjPrim_SetTexture(objHitbox,hitbox);
	ObjRender_SetBlendType(objHitbox,BLEND_ALPHA);
	Obj_SetRenderPriorityI(objHitbox,50);
	ObjRender_SetAlpha(objHitbox,0);
	ObjRender_SetScaleXYZ(objPlayer,1,1,0);
	ObjSprite2D_SetSourceRect(objHitbox,0,0,64,64);
	ObjSprite2D_SetDestCenter(objHitbox);

	while(!Obj_IsDeleted(objHitbox)){
		// Setting stuff
		ObjRender_SetPosition(objHitbox,GetPlayerX,GetPlayerY,0);
		ObjRender_SetAngleZ(objHitbox,angleS);
		ObjRender_SetAlpha(objHitbox,hitboxAlpha);

		angleS += 2; // make it spin 

		if(isFocus){
			hitboxAlpha = 255;
		}
		else {
			hitboxAlpha = 0;
		}

		yield;
	}
}

//Are you expecting secrets to be hidden here? Well, guess what?
//I just so happen to have one for you!
//https://www.youtube.com/watch?v=FixlRkpx_1c >:)
